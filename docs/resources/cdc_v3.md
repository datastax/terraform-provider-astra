---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "astra_cdc_v3 Resource - terraform-provider-astra"
subcategory: ""
description: |-
  astra_cdc_v3 enables CDC between Astra Serverless database and Astra Streaming.
---

# astra_cdc_v3 (Resource)

`astra_cdc_v3` enables CDC between Astra Serverless database and Astra Streaming.

## Example Usage

```terraform
/*
NOTE:

The streaming tenant and database will be created at the same time because they have no dependent resources in the flow.
The table will then be created, and then the CDC connection after that. This all follows terraform dependency rules.
*/

# Generate a random pet name to avoid naming conflicts
resource "random_uuid" "identifier" {}

# Create a new tenant
resource "astra_streaming_tenant" "streaming_tenant1" {
  cluster_name = "pulsar-gcp-uscentral1"
  tenant_name  = "streaming-uscentral1"
  user_email   = "someuser@example.com"
}

resource "astra_streaming_tenant" "streaming_tenant2" {
  cluster_name = "pulsar-gcp-useast1"
  tenant_name  = "streaming-useast1"
  user_email   = "someuser@example.com"
}

# Create a new database
resource "astra_database" "db_database" {
  name                = "mydb"
  keyspace            = "ks1"                       # 48 characters max
  cloud_provider      = "gcp"                       # this must match the cloud_provider of the streaming tenants
  regions             = ["us-central1", "us-east1"] # this must match the regions of the streaming tenants
  deletion_protection = false
}

# Create a new table in that database
resource "astra_table" "db_table1" {
  keyspace           = astra_database.db_database.keyspace
  database_id        = astra_database.db_database.id
  region             = astra_database.db_database.regions[0]
  table              = "table1"
  clustering_columns = "click_timestamp"
  partition_keys     = "visitor_id:click_url"
  column_definitions = [
    {
      Name : "visitor_id"
      Static : false
      TypeDefinition : "uuid"
    },
    {
      Name : "click_url"
      Static : false
      TypeDefinition : "text"
    },
    {
      Name : "click_timestamp"
      Static : false
      TypeDefinition : "bigint"
    },
  ]
}

# Create a new CDC connection between tenant topic and db table
resource "astra_cdc_v3" "db_cdc" {
  database_id   = astra_database.db_database.id
  database_name = astra_database.db_database.name
  tables = [
    {
      keyspace = astra_table.db_table1.keyspace
      table    = astra_table.db_table1.table
    },
  ]
  regions = [
    {
      region            = "us-central1"
      datacenter_id     = "${astra_database.db_database.id}-1"
      streaming_cluster = astra_streaming_tenant.streaming_tenant1.cluster_name
      streaming_tenant  = astra_streaming_tenant.streaming_tenant1.tenant_name
    },
    {
      region            = "us-east1"
      datacenter_id     = "${astra_database.db_database.id}-2"
      streaming_cluster = astra_streaming_tenant.streaming_tenant2.cluster_name
      streaming_tenant  = astra_streaming_tenant.streaming_tenant2.tenant_name
    },
  ]
}

output "streaming_data_topic1" {
  value = astra_cdc_v3.example1.data_topics["us-east1"]["ks1.table1"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `database_id` (String) Astra database to create the keyspace.
- `database_name` (String) Astra database name.
- `regions` (Attributes Set) Mapping between datacenter regions and streaming tenants. (see [below for nested schema](#nestedatt--regions))
- `tables` (Attributes Set) List of tables to enable CDC.  Must include at least 1. (see [below for nested schema](#nestedatt--tables))

### Read-Only

- `data_topics` (Map of Map of String) Map of CDC data topics for each table in each region. Use the region as the first key and the keyspace.table as the second key. For example, astra_cdc.mycdc.data_topics['us-east1']['ks1.table1'].

<a id="nestedatt--regions"></a>
### Nested Schema for `regions`

Required:

- `datacenter_id` (String) Astra Datacenter ID
- `region` (String) Cloud provider region
- `streaming_cluster` (String) Name of Pulsar cluster hosting the streaming tenant.
- `streaming_tenant` (String) Name of the streaming tenant


<a id="nestedatt--tables"></a>
### Nested Schema for `tables`

Required:

- `keyspace` (String)
- `table` (String)

## Import

Import is supported using the following syntax:

```shell
# The ID used for the import is the database UUID
terraform import astra_cdc_v3.example 48bfc13b-c1a5-48db-b70f-b6ef9709872b
```
